buildscript {
  ext.kotlin_version = '1.2.21'

  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2' // Workaround for https://github.com/johnrengelman/shadow/issues/336
  }
}

plugins {
  id 'java'
  id 'application'
  id 'de.undercouch.download' version '3.3.0' // For the download tasks
  id 'eclipse' // Support for Eclipse projects
  id 'idea' // Support for IntelliJ IDEA projects
}
apply plugin: 'kotlin'

mainClassName = "org.toradocu.Toradocu"
apply plugin: 'com.github.johnrengelman.shadow' // Workaround for https://github.com/johnrengelman/shadow/issues/336

task downloadGlove(type: Download) {
  src 'http://star-rep.inf.usi.ch/alberto/glove/raw/master/glove-binary.zip'
  dest 'src/main/resources/glove-binary.zip'
  onlyIfNewer true
  overwrite false
}

task unzipGlove(dependsOn: downloadGlove, type: Copy) {
  def zipFile = file('src/main/resources/glove-binary.zip')
  def outputDir = file('src/main/resources')
  from zipTree(zipFile)
  into outputDir
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task downloadGloveModels(type: Download) {
  src 'http://star-rep.inf.usi.ch/alberto/glove/raw/master/glove-models.zip'
  dest 'src/main/resources/glove-models.zip'
  onlyIfNewer true
  overwrite false
}

task unzipGloveModels(dependsOn: downloadGloveModels, type: Copy) {
  def zipFile = file('src/main/resources/glove-models.zip')
  def outputDir = file('src/main/resources')
  from zipTree(zipFile)
  into outputDir
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources.dependsOn(unzipGlove, unzipGloveModels)

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.0'
jar {
  manifest {
    attributes 'Implementation-Title': 'Toradocu', 'Implementation-Version': version
  }
}

sourceSets {
  main {
    java {
      excludes = ['org/toradocu/util/old/*']
    }
  }
  test {
    java {
      excludes = ['org/toradocu/util/old*',
                  'org/toradocu/regression/*',
                  'org/toradocu/generator/**']
    }
    resources {
      exclude 'goal-output/issta_2018'
    }
  }
}

test {
  exclude 'org/toradocu/generator/**'
  exclude 'org/toradocu/accuracy/paper/*'
  reports {
    html.enabled = true
  }
  // Print standard output during tests execution.
  testLogging {
    events = ['STANDARD_OUT', 'FAILED']
    exceptionFormat = 'FULL'
  }
}

task issta18 (type: Test) {
  include 'org/toradocu/accuracy/paper/*'
  enableAssertions = false
  ignoreFailures = true
}

/* Pass Java system properties from the command line to test tasks. */
tasks.withType(Test) {
  systemProperties System.getProperties()
}

/* Create Javadoc documentation as part of build task. */
build.dependsOn javadoc

javadoc {
  options.encoding = 'UTF-8'
  exclude 'org/toradocu/generator/**'
  exclude 'org/toradocu/util/old/**'
}

/* Disable unneeded tasks. */
startScripts.enabled = false
distZip.enabled = false
distTar.enabled = false
assemble.enabled = false
// To exclude shadowJar task from build: ./gradlew build -PdisableShadowJar
if (project.hasProperty('disableShadowJar')) {
  shadowJar.enabled = false
}
startShadowScripts.enabled = false
shadowDistZip.enabled = false
shadowDistTar.enabled = false

compileJava {
  options.compilerArgs << '-Xlint:unchecked,deprecation'
}

repositories {
  mavenCentral()
}

dependencies {
  compile fileTree(dir: 'lib', include: '*.jar')
  compile files("${System.properties['java.home']}/../lib/tools.jar")
  compile 'edu.stanford.nlp:stanford-corenlp:3.6.0'
  compile 'edu.stanford.nlp:stanford-corenlp:3.6.0:models-english'
  compile 'de.jungblut.math:tjungblut-math:1.3'
  compile 'de.jungblut.common:thomasjungblut-common:1.1'
  compile 'org.deeplearning4j:deeplearning4j-nlp:0.8.0'
  compile 'org.nd4j:nd4j-native-platform:0.8.0'
  compile 'org.jsoup:jsoup:1.8.3' // JSoup used only to remove HTML tags in comments.
  compile 'com.github.javaparser:javaparser-core:3.5.4'
  compile 'com.beust:jcommander:1.69'
  compile 'com.google.code.gson:gson:2.8.0'
  compile 'commons-io:commons-io:2.5'
  compile 'org.slf4j:slf4j-simple:1.7.21'
  compile 'org.apache.commons:commons-lang3:3.4'
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
}

// The following configuration excludes sfl4j->log4j binding that is added by de.jungblut.common
// dependency.
configurations.all {
  exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.3.1'
}

import de.undercouch.gradle.tasks.download.Download

/* Downloads binaries of the systems used to measure Toradocu's precision and recall. */
task downloadBinaries(type: Download) {
  src ([
    'http://repo1.maven.org/maven2/org/apache/commons/commons-collections4/4.1/commons-collections4-4.1.jar',
    'http://repo1.maven.org/maven2/org/apache/commons/commons-math3/3.6.1/commons-math3-3.6.1.jar',
    'http://repo1.maven.org/maven2/com/google/guava/guava/19.0/guava-19.0.jar',
    'http://repo1.maven.org/maven2/org/jgrapht/jgrapht-core/0.9.2/jgrapht-core-0.9.2.jar',
    'https://github.com/mernst/plume-lib/releases/download/v1.1.0/plume-lib-1.1.0.tar.gz',
    'http://www.inf.usi.ch/phd/goffi/toradocu/freecol-0.11.6.jar',
    'http://central.maven.org/maven2/commons-cli/commons-cli/1.3.1/commons-cli-1.3.1.jar',
    'http://repo1.maven.org/maven2/org/graphstream/gs-core/1.3/gs-core-1.3.jar'
  ])
  dest 'src/test/resources/bin'
  onlyIfNewer true
  overwrite false
}

/* Extracts binaries that were not already downloaded directly. */
task extractBinaries(type: Exec, dependsOn: 'downloadBinaries') {
  description "Extract .jar files from downloaded files"
  workingDir 'src/test/resources/bin'
  commandLine "bash", "-c", "tar xzf plume-lib-1.1.0.tar.gz --strip-components=2 plume-lib-1.1.0/java/plume.jar && mv plume.jar plume-lib-1.1.0.jar"
}

/* Downloads sources of the systems used to measure Toradocu's precision and recall. */
task downloadSources(type: Download) {
  src ([
    'https://www.apache.org/dist/commons/collections/source/commons-collections4-4.1-src.zip',
    'https://www.apache.org/dist/commons/math/source/commons-math3-3.6.1-src.zip',
    'http://repo1.maven.org/maven2/com/google/guava/guava/19.0/guava-19.0-sources.jar',
    'http://repo1.maven.org/maven2/org/jgrapht/jgrapht-core/0.9.2/jgrapht-core-0.9.2-sources.jar',
    'https://github.com/mernst/plume-lib/releases/download/v1.1.0/plume-lib-1.1.0.tar.gz',
    'http://www.inf.usi.ch/phd/goffi/toradocu/freecol-0.11.6-src.zip',
    'http://repo1.maven.org/maven2/org/graphstream/gs-core/1.3/gs-core-1.3-sources.jar'
  ])
  dest 'src/test/resources/src'
  onlyIfNewer true
  overwrite false
}

/* Extracts source archives of the systems used to measure Toradocu's precision and recall. */
task extractSources(dependsOn: downloadSources) {
  doLast {
    file('src/test/resources/src').listFiles().findAll {
      it.name.endsWith('.zip') || it.name.endsWith('.tar.gz') || it.name.endsWith('.jar')
    }.each { file ->
      if (file.name.endsWith('.zip')) {
        copy {
          from zipTree(file)
          into 'src/test/resources/src/'
          fileMode 0664
        }
      } else if (file.name.endsWith('.tar.gz')) {
        copy {
          from tarTree(resources.gzip(file))
          into 'src/test/resources/src/'
          fileMode 0664
        }
      } else if (file.name.endsWith('.jar')) {
        def fileName = file.name
        def index = fileName.lastIndexOf('.')
        def fileBaseName = fileName.substring(0, index)
        copy {
          from zipTree(file)
          into 'src/test/resources/src/' + fileBaseName
          fileMode 0664
        }
      }
    }
  }
}

/* Workaround for the Javaparser bug: https://github.com/javaparser/javaparser/issues/990 */
task cleanSource(type: Exec, dependsOn: 'extractSources') {
    commandLine './clean.sh'
}

task removeTestOutputDirs(type: Delete) {
  delete 'build/test-results', 'aspects'
}

test.dependsOn removeTestOutputDirs, cleanSource, extractBinaries

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  environment PATH: "$System.env.PATH:$buildDir/utils/plume-lib/bin"
  commandLine "bash", "-c", "find src/ -path src/test/resources -prune -o -name '*.java' | sort-directory-order | xargs etags"
}

/* Install the git pre-commit hook */
task installGitHook(type: Copy) {
  from(new File(rootProject.rootDir, "git-pre-commit-hook.sh")) {
    rename({ return "pre-commit" })
  }
  into { new File(rootProject.rootDir, ".git/hooks") }
}
build.dependsOn(installGitHook)

shadowJar {
  // default: "${baseName}-${version}-${classifier}.${extension}",
  // where ${baseName} is the name of the directory into which the toradocu
  // repository is cloned.
  archiveName = "toradocu-${version}-${classifier}.${extension}"
}

task getCodeFormatScripts(type: Exec) {
  description "Obtain the run-google-java-format scripts"
  commandLine "bash", "-c",
          "[ -d .run-google-java-format ] " +
          "&& (cd .run-google-java-format && git pull -q) " +
          "|| git clone -q https://github.com/plume-lib/run-google-java-format.git .run-google-java-format"
}

task checkFormat(type: Exec) {
  description "Check whether the source code is properly formatted"
  commandLine "bash", "-c",
          "find src -name \"*.java\" -type f " +
          "-not -path \"src/test/resources/*\" " +
          "| xargs ./.run-google-java-format/check-google-java-format.py " +
	  "|| (echo 'Try running:  gradle reformat' && false)"
}
checkFormat.dependsOn getCodeFormatScripts
build.dependsOn checkFormat

/* Format the code according to the Google Java format code style */
task reformat(type: Exec) {
  description "Format the Java source code"
  commandLine "bash", "-c",
          "find src -name \"*.java\" -type f " +
          "-not -path \"src/test/resources/*\" " +
          "| xargs ./.run-google-java-format/run-google-java-format.py"
}
reformat.dependsOn getCodeFormatScripts

/* Downloads dependencies needed for the tutorial. */
task tutorial(type: Download) {
  src ([
    'http://search.maven.org/remotecontent?filepath=junit/junit/4.12/junit-4.12.jar',
    'http://search.maven.org/remotecontent?filepath=org/hamcrest/hamcrest-core/1.3/hamcrest-core-1.3.jar',
    'http://central.maven.org/maven2/org/aspectj/aspectjtools/1.8.9/aspectjtools-1.8.9.jar',
    'http://central.maven.org/maven2/org/aspectj/aspectjweaver/1.8.9/aspectjweaver-1.8.9.jar',
    'http://central.maven.org/maven2/org/aspectj/aspectjrt/1.8.9/aspectjrt-1.8.9.jar'
  ])
  dest 'tutorial'
  onlyIfNewer true
  overwrite false
}
tutorial.dependsOn shadowJar
